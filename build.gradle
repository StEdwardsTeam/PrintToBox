plugins {
  id "nebula.os-package" version "2.2.6"
}

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'os-package'
version = '1.5'
mainClassName='PrintToBox'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.4'
    compile 'com.box:box-java-sdk:1.1.0'
    compile 'commons-cli:commons-cli:1.3.1'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

ospackage  {
    release = '1'
    os = 'LINUX'
    packageName = 'printtobox'
    summary = 'Upload files for enterprise users to collaborated folders in Box.com'
    packager = 'Stephen Olander-Waters <stephenw@stedwards.edu>'
    maintainer = 'Stephen Olander-Waters <stephenw@stedwards.edu>'
    url = 'https://github.com/StEdwardsTeam/PrintToBox'
    license = 'MIT'
    user = 'root'
    permissionGroup = 'root'
    requires('coreutils')
    preInstall = file('preInstall.sh')
    postInstall = file('postInstall.sh')
    postUninstall = file('postUninstall.sh')

    from ('build/install/PrintToBox/lib') {
        into '/usr/lib/PrintToBox/lib'
        user 'root'
        fileMode 0644
    }
    from ('build/install/PrintToBox/bin') {
        into '/usr/lib/PrintToBox/bin'
        user 'root'
        fileMode 0755
    }
    from ('PrintToBox.conf') {
        fileType CONFIG | NOREPLACE
        into '/etc'
        user 'root'
        permissionGroup 'printtobox'
        fileMode 0640
    }
    from ('tokens') {
        fileType CONFIG | NOREPLACE
        into '/var/cache/PrintToBox'
        user 'root'
        permissionGroup 'printtobox'
        fileMode 0660
    }
    from (['LICENSE','README.md']) {
        into '/usr/share/doc/PrintToBox'
        user 'root'
        fileMode 0644
    }
}

task createDeb(type: Deb, dependsOn: [installApp]) {
    configurationFile('''/etc/PrintToBox.conf
/var/cache/PrintToBox/tokens
''')
    requires('openjdk-7-jre-headless | java7-runtime-headless | jre')
    requires('passwd')
}

task createRpm(type: Rpm, dependsOn: [installApp]) {
    requires('shadow-utils')
}
