plugins {
  id "nebula.ospackage" version "4.4.0"
}

apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'nebula.ospackage'
version = '2.2'
mainClassName='PrintToBox'

repositories {
    mavenCentral()
// Only necessary for using Box SDK development snapshots
//    maven {
//        url "https://oss.sonatype.org/content/repositories/snapshots/"
//    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.5'
    compile 'com.box:box-java-sdk:2.5.0'
    compile 'commons-cli:commons-cli:1.3.1'
    runtime 'org.slf4j:slf4j-api:1.7.12'
    runtime 'org.slf4j:slf4j-simple:1.7.12'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

ospackage  {
    release = '1'
    os = 'LINUX'
    packageName = 'printtobox'
    packageGroup = 'Utilities'
    summary = 'Upload files for enterprise users to collaborated folders in Box.com'
    packager = 'Stephen Olander-Waters <stephenw@stedwards.edu>'
    maintainer = 'Stephen Olander-Waters <stephenw@stedwards.edu>'
    vendor = 'SEU'
    url = 'https://github.com/stedwards/PrintToBox'
    license = 'MIT'
    user = 'root'
    permissionGroup = 'root'
    requires('coreutils')
    preInstall = file('preInstall.sh')

    from ('build/install/PrintToBox/lib') {
        into '/usr/lib/PrintToBox/lib'
        user 'root'
        fileMode 0644
    }
    from ('build/install/PrintToBox/bin') {
        into '/usr/lib/PrintToBox/bin'
        user 'root'
        fileMode 0755
    }
    from ('PrintToBox.conf') {
        fileType CONFIG | NOREPLACE
        into '/etc/PrintToBox'
        user 'root'
        permissionGroup 'printtobox'
        fileMode 0640
    }
    from (['LICENSE','README.md']) {
        into '/usr/share/doc/PrintToBox'
        user 'root'
        fileMode 0644
    }
}

task createDeb(type: Deb, dependsOn: [installDist]) {
    configurationFile('''/etc/PrintToBox/PrintToBox.conf
''')
    requires('openjdk-7-jre-headless | java7-runtime-headless | jre')
    requires('passwd')

    link('/usr/bin/PrintToBox', '/usr/lib/PrintToBox/bin/PrintToBox')
}

task createRpm(type: Rpm, dependsOn: [installDist]) {
    requires('shadow-utils')

    link('/usr/bin/PrintToBox', '/usr/lib/PrintToBox/bin/PrintToBox')
}
